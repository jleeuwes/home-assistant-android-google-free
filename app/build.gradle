apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'

buildscript {
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.5.2.0'
    }
}


android {
    compileSdkVersion projectSdkVersion.toInteger()

    defaultConfig {
        applicationId "eu.lwstn.houseautomator.comrade.android"
        minSdkVersion projectMinSdkVersion
        targetSdkVersion 29
        // versionCode needs to be incremented for each release, regardless of the version it is based on
        versionCode 1
        // versionName starts with versionCode, then the upstream version it is based on
        versionName "1-based-on-1.8.0"
    }

    signingConfigs {
        release {
            storeFile file('release_keystore.keystore')
            storePassword "${System.env.KEYSTORE_PASSWORD ?: ""}"
            keyAlias "$System.env.KEYSTORE_ALIAS" ?: ""
            keyPassword "${System.env.KEYSTORE_ALIAS_PASSWORD ?: ""}"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation project(':common')
    implementation project(':domain')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "com.google.dagger:dagger:${daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"

    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutversion"
    implementation "androidx.preference:preference-ktx:1.1.0"
    implementation 'com.google.android.material:material:1.1.0'

    implementation("com.jakewharton.threetenabp:threetenabp:$threeTenAbpVersion") {
        exclude group: 'org.threeten'
    }

    implementation "androidx.work:work-runtime-ktx:2.3.2"

    // Added some dependencies that were missing after removing lokalise and fabric.
    // Used the versions reported by Android Studio when lokalise and fabric were included.
    implementation("com.squareup.okhttp3:okhttp:4.2.2")
    implementation 'com.google.code.gson:gson:2.7'

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek2Version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek2Version"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
}
